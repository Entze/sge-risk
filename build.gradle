plugins {
    id 'java'
    id 'idea'
    id "maven-publish"
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'at.ac.tuwien.ifs.sge'
version '1.0.3'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/Entze/Strategy-Game-Engine")
        credentials {
            username = project.findProperty("gpr.user") ?: findProperty("github.actor") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    implementation 'org.jgrapht:jgrapht-core:1.5.1'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'org.yaml:snakeyaml:1.29'
    implementation 'info.picocli:picocli:4.6.1'
    implementation 'at.ac.tuwien.ifs.sge:sge:1.0.3'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.pholser:junit-quickcheck-core:1.0'
    testImplementation 'com.pholser:junit-quickcheck-generators:1.0'
}


jar {
    manifest {
        attributes 'Sge-Type': 'game'
        attributes 'Game-Class': 'at.ac.tuwien.ifs.sge.game.risk.board.Risk'
        attributes 'Main-Class': 'at.ac.tuwien.ifs.sge.engine.risk.RiskMapGenerator'
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

shadowJar {
    archiveClassifier.set('exe');
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}


publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            pom {
                name = project.getName()
                description = "A clone of the game \"Risk\" implemented in sge."
                licenses {
                    license {
                        name = "GNU Affero General Public License version 3"
                        url = "https://opensource.org/licenses/agpl-v3.html"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "entze"
                        name = "Lukas Grassauer"
                        email = "entze@grassauer.eu"
                    }
                }

                scm {
                    url = "https://github.com/Entze/sge-risk"
                    connection = "https://github.com/Entze/sge-risk.git"
                    developerConnection = "git@github.com:Entze/sge-risk.git"
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Entze/sge-risk")
            credentials {
                username = project.findProperty("gpr.user") ?: findProperty("github.actor") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

test {

    useJUnit()
    maxHeapSize = '1G'
}
